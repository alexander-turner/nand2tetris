// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // Handle zx 
    Not16(in=x, out=notX);
    And16(a=x, b=notX, out=zero); // x and notx always = false (0)
    Mux16(a=x, b=zero, sel=zx, out=xZX);
    
    // Handle bitwise not 
    Not16(in=xZX, out=notxZX);
    Mux16(a=xZX, b=notxZX, sel=nx, out=xNX);

    // Handle yzero 
    Mux16(a=y, b=zero, sel=zy, out=yZY);

    // Negate y? 
    Not16(in=yZY, out=notyZY);
    Mux16(a=yZY, b=notyZY, sel=ny, out=yNY);

    // Which function 
    Add16(a=xNX, b=yNY, out=xySum);
    And16(a=xNX, b=yNY, out=xyAnd);
    Mux16(a=xyAnd, b=xySum, sel=f, out=fOut);

    // Bitwise not the output? 
    Not16(in=fOut, out=nfOut);
    Mux16(a=fOut, b=nfOut, sel=no, out=out);
    Mux16(a=fOut, b=nfOut, sel=no, out=internalOut);

    // Check if out is zero
    Or16(in=internalOut, out=orOut);
    Not(in=orOut, out=zr);
    
    // Check if negative 
    Or(a=internalOut[15], b=false, out=ng); // Two's complement, most sign. bit indicates negative
}


// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl
/** 
 * The Memory chip implements the complete address space of the Hack data memory,
 * including RAM, memory mapped screen, and memory mapped keyboard.
 * Outputs the value of the memory location specified by the address input.
 * If (load == 1), sets the memory location specified by the address input
 * to the value of the in input.
 * Address space rules:
 * Only the upper 16K + 8K + 1 words of the memory are used.
 * Access to address 0 to 16383 (0x0000 to 0x3FFF) results in accessing the RAM;
 * Access to address 16384 to 24575 (0x4000 to 0x5FFF) results in accessing the Screen memory map;
 * Access to address 24576 (0x6000) results in accessing the Keyboard memory map.
 */ 
CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // Make 32K ram chip, have outs go to appropriate device 
    // Check if accessing 16K ram
    Not(in=address[14], out=accessRAM);
    And(a=accessRAM, b=load, out=loadRAM);
    RAM16K(in=in, load=loadRAM, address=address[0..13], out=outRAM); // But how know which part?
    
    // Check if accessing Screen (2 MSB are 1 and 0, respectively)
    Not(in=address[13], out=notKeyboard); 
    And(a=address[14], b=notKeyboard, out=accessScreen);
    And(a=accessScreen, b=load, out=loadScreen);
    Screen(in=in, load=loadScreen, address=address[0..12], out=outScreen);

    And(a=address[14], b=address[13], out=accessKeyboard);
    // And(a=accessKeyboard, b=load, out=loadKeyboard);
    Keyboard(out=outKeyboard);

    Mux16(a=outRAM, b=outScreen, sel=accessScreen, out=outMux);
    Mux16(a=outMux, b=outKeyboard, sel=accessKeyboard, out=out);

}
